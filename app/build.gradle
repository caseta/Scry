apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: 'buildscripts/jacoco.gradle'

def versionMajor = 1
def versionMinor = 2
def versionPatch = 0

def appBuildVersion = versionMajor * 1000000 + versionMinor * 1000 + versionPatch * 100
def appVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
  flavorDimensions "default"
  compileSdkVersion 28
  lintOptions {
    abortOnError false
  }

  buildToolsVersion "28.0.3"

  defaultConfig {
    multiDexEnabled true
    applicationId "com.taylorcase.hearthstonescry"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode appBuildVersion
    versionName appVersionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    def debugKey = "\"debugKey\""
    def apiKey = project.hasProperty('HEARTHSTONE_API_KEY') ? HEARTHSTONE_API_KEY : debugKey
    if (apiKey == null) {
      apiKey = debugKey
    }
    buildConfigField "String", "HEARTHSTONE_API_KEY", apiKey
  }

  androidExtensions {
    experimental = true
  }

  buildTypes {
    release {}

    debug {
      ext.enableCrashlytics = false
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      all {
        testLogging {
          events "passed", "skipped", "failed", "standardOut", "standardError"
        }
      }
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
  implementation "com.rengwuxian.materialedittext:library:${materialEditTextVersion}"
  implementation "com.jakewharton.timber:timber:${timberVersion}"
  implementation "com.github.bumptech.glide:glide:${glideVersion}"
  annotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"

  //  Retrofit
  implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
  implementation "com.squareup.retrofit2:converter-gson:${gsonVersion}"
  implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:${retrofit2Rxjava2AdapterVersion}"

  //  Dagger
  implementation "com.google.dagger:dagger:${daggerVersion}"
  annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
  kapt "com.google.dagger:dagger-compiler:${daggerVersion}"

  //  Rx
  implementation "io.reactivex.rxjava2:rxjava:${rxJava2Version}"
  implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
  implementation "com.jakewharton.rxbinding2:rxbinding:${rxBindingVersion}"

  // Support
  implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"
  implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
  implementation "com.android.support:design:${supportLibVersion}"
  implementation "com.android.support:support-annotations:${supportLibVersion}"
  implementation "com.android.support:appcompat-v7:${supportLibVersion}"
  implementation("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}@aar") {
    transitive = true
  }
  implementation "com.android.support:cardview-v7:${supportLibVersion}"

  //  Arch
  implementation "android.arch.persistence.room:runtime:${archRoomVersion}"
  implementation "android.arch.persistence.room:rxjava2:${archRoomVersion}"
  kapt "android.arch.persistence.room:compiler:${archRoomVersion}"
  annotationProcessor "android.arch.persistence.room:compiler:${archRoomVersion}"

  // Reporting
  implementation "com.google.firebase:firebase-core:${firebaseVersion}"
  implementation "com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}"

  //  Testing
  testImplementation "junit:junit:${junitVersion}"
  testImplementation "org.robolectric:robolectric:${robolectricVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
  testImplementation "com.nhaarman:mockito-kotlin:${mockitoKotlinVersion}"
  testImplementation "com.squareup.assertj:assertj-android:${assertjVersion}"
}

apply plugin: 'com.google.gms.google-services'
